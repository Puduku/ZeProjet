// blotex.topo, version 1.92 (ANSI)
// (c) Atos-Euronext Belgium - 2008
// (c) Puduku - 2023

// Description: blot expressions management 
// Basic stuff allowing to add some "intelligence" (based on expression evaluation) in blotcode.
//
// NOTE: blotex lib provides "script-like programming" capabilites to blot code.
// Advantage: enhance flexibility of blot code, BUT keep in mind the drawback:
// scripting features means natural tendency to produce complex (or tricky) code (solely
// maintenable by programmers), yet the initial (laudable) intention of blot code was to
// remain fairly legible by non computer experts... 


// Using blotex in blot code
// =========================
//
// I. Blot expressions (blotex) 
// ----------------------------
// Legend:
// - <entity name>   => "soft" lexeme ; leading white spaces REMOVED 
// - <<single char>> => "hard" lexeme ; leading white spaces NOT REMOVED
//
// There two types of blot expressions: 
// - "string" blotex => "strex" type
// - "blotval" blotex: signed integer => "intex" type
// <blotex> ::= <intex> | <strex> 
//
// Blot expressions are made of "atoms" :
// <intex> ::= <intex atom> ( <int 2op> <intex> )*  
// <strex> ::= <strex atom> ( <str 2op> <strex> )*  
//
// Expressions operators allows to combine expression atoms together 
// <int 1op> ::= '!' | '+' | '-' 
// <int 2op> ::= '+' | '-' | '*' | '/' | '&&' | '||' | '>' | '<' | '>=' | '<=' | '==' | '!='
// Logical operators ('!', '&&' and '||') combine integer expressions together, "seen" as booleans.
//
// <str 2op> ::= '+' | '>' | '<' | '>=' | '<=' | '==' | '!=' | '===' 
// '+' operator applied on strings is a concatenation
//
// The atoms are made of constants, variables and "register operations".
// <intex atom> ::= [ <int 1op> ] ( <int constant> | <blotvar as int> | <blotvar entry> |
//   <blotvar id> | <blotreg int> | <blottab int> ) 
// <strex atom> ::= <str constant> | <blotvar as str> | <blotvar name> | <blotreg str> | <blottab str> 
//
// Constant expressions
// <int constant> : any integer constants of C language... 
// <str constant> ::= '"' ( <<single char>> )* '"'
// String example: "Julie" 
// Integer examples: 69 , 0xCACABEBE
// Reminder: blotval's convention : 
// - 0 : represents "true" boolean value
// - !=0 : represents "false" boolean value
//
// Entities. The different entities:
// - blotreg (blot register)
// - blotvar (blot variable)
// - blottab (blot table)
// - blottab element
// are identified by their name (may be empty).
//
// <entity name> ::= ( <<letter>> | <<digit>> | '_' )*
//
// Variables and registers
// A blot variable (blotvar) stores a blotex value.
// A blot register (blotreg) is a collection of blovars
// (the register with empty entity name is called the global register)
//
// Formally a blotvar is referenced as following (in which the 1st entity name identifies the register) :
// <blotvar> ::= <entity name> ( '.' <entity name>  | '[' <intex> ']' ) 
// Blotvar examples: 
// .toto => toto blotvar of global register
// recettes.bolo => bolo blotvar of recettes register
// recettes[0] => 1st blotvar stored in recettes register 
// [1] => 2nd blotvar stored in global register 
// recettes[.toto] => recettes register's blotvar indexed by toto global blotvar   
//
// Blotvar implicit and explicit integer value
// A blotvar value is implicitly an integer value
// However, reference to integer value of a blotvar may be explicit
// <blotvar as int> ::= <blotvar> [ '#' ]
// Examples:
// .toto => toto global blotvar's integer value 
// .toto# => ALSO toto global blotvar's integer value
//
// Blotvar string value 
// <blotvar as str> ::= <blotvar> '$' 
// Examples:
// .toto$           => toto global blotvar string value 
// recettes[.toto]$ => string value of recettes register's blotvar indexed by toto global blotvar
//
// Blotvar name and entry
// <blotvar name> ::= <blotvar> '!$'
// <blotvar entry> ::= <blotvar> '!#'
// Examples
// .toto!$ => toto global blotvar's name, that is "toto"
// recettes[0]!$ => name of 1st blotvar stored in recettes register   
// recettes.bolo!# => index (entry) of bolo blotvar in recettes register
// .toto!# => index (entry) of toto global blotvar 
//
// Register operations 
// <blotreg int> ::= <entity name> '?' <blotreg ops int>  
// <blotreg ops int> ::= [ <blotreg op select> ] [ [ <blotreg op reset> ] [ <blotreg op next> ]
//   <blotreg op read int> ] 
// <blotreg op select> ::= ':' <blotreg request>
// <blotreg op reset> ::= '^'
// <blotreg op next> ::= '+'
// <blotreg op read int> ::= '=' [ ( '#' | '!#' ) ]
//
// <blotreg str> ::= <entity name> '?' <blotreg ops str>  
// <blotreg ops str> ::= [ <blotreg op select> ] [ <blotreg op reset> ] [ <blotreg op next> ]
//   <blotreg op read str>
// <blotreg op read str> ::= '=' ( '$' | '!$' )  
//
// Blotreg requests are made of "atoms" : 
// <blotreg request> ::= <blotreg request atom> ( <logical 2op> <blotreg request> )* ':?'
// <blotreg request atom> ::= <int blotreg request atom> | <str blotreg request atom>
// <int blotreg request atom> ::= ( '#' | '!#' ) <request comp op> <intex> 
// <str blotreg request atom> ::= ( '$' | '!$' ) <str request comp op> <strex> 
//
// <request comp op> ::= '>' | '<' | '>=' | '<=' | '==' | '!='
// <logical 2op> ::= 'and' | 'or'
// <str request comp op> ::=  <request comp op>  | '===' 
//
// Examples
// recettes?: !$ === "*bolo*" and # >= 69 :?       => new register selection 
// recettes?: !$ === "*bolo*" or $ === "*bolo*" :? => new register selection (2nd exemple)
// recettes?^                                      => single reset, ascending 
// recettes?+                                      => fetch next one
// recettes?=                                      => read current int value 
// recettes?=$                                     => read current str value 
// recettes?: !$ === "*bolo*" :?+=$                => read str value of 1st "bolo" recette  
// 
// Blot tables and blot table operations
//
// A blot set (blotset) is a fixed set of blot values. Each element of the set is identified by
// its name. 
// A blot table (blottab) is a collection of blotsets 
// (the table with empty entity name is called the global table)
//
// <blottab int> ::= '^' <entity name> '?' <blottab ops int>  
// <blottab ops int> ::= <blottab op create> | ( [ <blottab op select> ] [ <blottab op reset> ]
//   [ <blottab op insert> ] [ <blottab op next> ] [ <blottab op read int> ] ) 
// <blottab op create> ::=  '[' ( <entity name> )+ ']?' 
// <blottab op select> ::= ':' <blottab request> ':?'
// <blottab op reset> ::= '^'
// <blottab op next> ::= '+'
// <blottab op insert> ::= '@'
// <blottab op read int> ::= '=' <int blottab element>
//
// <int blottab element> ::= <entity name> [ '#' ] 
// <str blottab element> ::= <entity name> '$'
//
// <blottab str> ::= '^' <entity name> '?' <blottab ops str>  
// <blottab ops str> ::= [ <blottab op select> ] [ <blottab op reset> ] [ <blottab op next> ]
//   [ <blottab op insert> ] <blottab op read str>
// <blottab op read str> ::= '=' <str blottab element> 
//
// Like blotreg requests, blottab requests are made of "atoms" : 
// <blottab request> ::= <blottab request atom> ( <logical 2op> <blottab request> )*
// <blottab request atom> ::= <int blottab request atom> | <str blottab request atom>
// <int blottab request atom> ::= <int blottab element> <request comp op> <intex> 
// <str blottab request atom> ::= <str blottab element> <str request comp op> <strex> 
//
// Blottab operations examples: 
// ^recettes2?[ name price ]?                       => Create table with two elements
// ^recettes2?: name$ === "*bolo*" or price < 10 :? => new table selection 
// ^recettes?^                                      => single reset, ascending 
// ^recettes2?+                                     => fetch next one 
// .var$ := ^recettes?=name$                        => read current name 
// ^recettes2?=price := 15                          => update current price 
// ^recettes2?@                                     => insert new table record  
// ^recettes2?=name$ := "craboutchas"               => new set name 
//
//
// II. "blotex" library's functions (those to be placed in blot code)
// --------------------------------
//
// Eval ([ <blotvar as int> := ] <intex>)
// Eval ([ <blotvar as str> := ] <strex>)
// ---- 
//
// Evaluate the blotex and eventually assign the result into a blotvar.
//
// Passed:
// - blotvar: variable name (if passed)
//   If the blotvar was not existing, it is automatically allocated by the function
// - intex: blotex integer expression
// - strex: blotex string expression
//
// Possible abandonment reasons:
// - assignement error:
//   + mismatch between blotvar and blotex types
// - blotex evaluation error:
//   + syntax error / type error in blotex
//   + (syntax error) problem in arguments passed to be blot function 
//   + (type error) type mismatch between blotvar and blotex arguments of be blot function 
//   + unknown blotvar / blotex function in blotex expression 
//   + value error in blotex expression
//   + overflow error : 
//
// Blotval:
// - blotex is a strex => always 0 ("true" blotval)
// - blotex is a intex => blotex value 
//
// Surrogate: None 
//
// Examples:
//   Eval (recettes?: !$ === "*bolo*" and # >= 69 :?) => new register selection 
//   Eval (recettes?: !$ === "*bolo*" or $ === "*bolo*" :?) => new register selection (2nd exemple)
//   Eval (recettes?^) => single reset, ascending 
//   Eval (recettes?+) => fetch next one
//   Eval (var := recettes?=) => read current int value 
//   Eval (var$ := recettes?=$) => read current str value 
//   Eval (^recettes2?[ name price ]? => Create recettes2 table with two fields name and price 
//   Eval (^recettes2?: name$ == "*bolo*" or price < 10 :?) => new table selection 
//   Eval (^recettes?^) => single reset, ascending 
//   Eval (^recettes2?+) => fetch next one 
//   Eval (var$ := ^recettes?=name$) => read current name 
//   Eval (^recettes2?=price := 15) => update current price 
//   Eval (^recettes2?@) => insert new table record  
//   Eval (^recettes2?=name$ := "craboutchas") => new set name 
//
//
// OutputF (<format> ',' <blotex> ) 
// -------
//
// Replace blot with content, following format
//
// Passed:
// - format
//   <format> ::= '%' [ <modifiers> ] ( <alla c specifier> | <binary specifier> )
//   <alla c specifier> ::= ( 'd' | 's' | 'x' | 'X' ) 
//   <binary specifier> ::= ( 'LE' | 'BE' )
//     Binary specifier : output intex in raw binary
//     + 'LE' : little endian (Intel) order 
//     + 'BE' : big endian (Internet) order 
//     (see examples below) 
//     Reminder: intex type handles signed values (2 complement)
// - blotex: 
//   strex for 's' specifier 
//   intex otherwise 
//
// Possible abandonment reasons:
// - formatting error:
//   + wrong format 
//   + truncation error: 
// - blotex evaluation error:
//   + syntax error / type error in blotex
//   + (syntax error) problem in arguments passed to be blot function 
//   + (type error) type mismatch between blotvar and blotex arguments of be blot function 
//   + unknown blotvar / blotex function in blotex expression 
//   + value error in blotex expression
//   + overflow error : 
//
// Blotval (if not abandonned):
// 0 ("true") 
//
// Surrogate: output content as specified in parameters... 
//
// Examples:
//   OutputF (%d, var) 
//   OutputF (%03d, var) 
//   OutputF (%04X, var) 
//   OutputF (%4LE, var) => little endian, four bytes 
//    little endian (less significant byte at lowest address) is compliant with X86 (Intel) hardware 
//   OutputF (%8BE, var) => big endian (network order), four bytes 
//    big endian (most significant byte at lowest address) is compliant with historical UNIX
//    hardware (Motorola 68000, Sun SPARC); big endian corresponds to the (Internet) network byte
//    order. 
//   OutputF (%s, var$) 
//   OutputF (%.10s, var$) 

